/*+**************************************************************************/
/***                                                                      ***/
/***   This file is distributed under a BSD license.                      ***/
/***   See LICENSE.txt for details.                                       ***/
/***                                                                      ***/
/**************************************************************************+*/

level11;

asc
{
  cbuffer CSTestPara : register (c0) : slot cs 0
  {
    float4 tx;
    float4 ty;
    float4 ofs;
    float4 pot0;
    float4 pot1;
    float4 pot2;
    float4 pot3;
  };
}

cs CSTest
{
  asc cs_5_0
  {
    use CSTestPara;
    RWByteAddressBuffer vb : register(u0);
    RWStructuredBuffer<uint> countbuffer : register(u1);

    [numthreads(4,4,4)]
    void main
    (
      uint3 tid : SV_GroupThreadID,
      uint3 gid : SV_GroupID
    )
    {
      uint3 id = tid+gid*4;
      float3 p = float3(id)+ofs.xyz;
      
      float pot = pot0.w/length(p-pot0.xyz)
                + pot1.w/length(p-pot1.xyz)
                + pot2.w/length(p-pot2.xyz)
                + pot3.w/length(p-pot3.xyz);
                
      float s = saturate((pot-0.6f)*8);
      if(s>0)
      {      
        uint n = countbuffer.IncrementCounter();
        vb.Store4(n*16,asuint(float4(p,s)));
      }
    }
  }
}

cs WriteCount
{
  asc cs_5_0
  {
    RWStructuredBuffer<uint> cnt : register(u0);
    RWBuffer<uint> ind : register(u1);
    [numthreads(1,1,1)]
    void main()
    {
      ind[0] = 36;
      ind[1] = cnt.IncrementCounter();
      ind[2] = 0;
      ind[3] = 0;
      ind[4] = 0;
    }
  }
}

/**************************************************************************+*/

asc
{
  cbuffer MaterialFlatPara : register(c0)
  {
    row_major float4x4 mvp;
    float4 ldir;
  };
}

material MaterialFlat
{
  // the vertex shader

  vs
  {    
    asc vs_5_0                 // hlsl code
    {
      use MaterialFlatPara;

      void main
      (
        in float3 in_pos : POSITION,            // input, from vertex array
        in float3 in_norm : NORMAL,
        in float2 in_uv : TEXCOORD0,
        in float4 in_inst : TEXCOORD1,
        out float2 out_uv : TEXCOORD0,          // output, to pixel shader. 
        out float4 out_col : COLOR0,
        out float4 out_pos : POSITION,          // position last for DX11
      ) 
      {
        out_uv = in_uv;
        float l = saturate(in_norm ° ldir.xyz); // this is a dot product!
        out_col = float4(l,l,l,1);
        float3 pos = in_pos*in_inst.w+in_inst.xyz;
        out_pos = mul(float4(pos,1),mvp);
      }
    }
  }

  // the pixel shader

  ps
  {

    asc ps_5_0
    {
      SamplerState sam0 : register(s0);
      Texture2D tex0 : register(t0);
      void main
      (
        in float2 uv : TEXCOORD0,
        in float4 col : COLOR0,
        out float4 result : COLOR0
      )
      {
        result = col*tex0.Sample(sam0,uv);
      }
    }
  }
};
